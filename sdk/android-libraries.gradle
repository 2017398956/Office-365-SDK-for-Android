apply plugin: 'maven'

def myProject = project

uploadArchives {
    repositories.mavenDeployer {
        pom.groupId = myProject.group
        pom.artifactId = PUBLISH_ARTIFACT_ID
        pom.version = myProject.version
        repository (
                url: 'https://api.bintray.com/maven/msopentech/Maven/Office-365-SDK-for-Android'
        ) {
            authentication(
                    userName: 'user',
                    password: 'foo'
            )
        }
    }
}

android.libraryVariants.all { variant ->

    def task = project.tasks.create "jar${name.capitalize()}", Jar
    task.dependsOn variant.javaCompile
    task.from variant.javaCompile.destinationDir
    task.from configurations.compile.findAll {

        it.getName() != 'android.jar' &&
        //manually removing dependencies. .jar consumers will need to fulfill these dependencies
        !it.getName().startsWith('guava') &&
        !it.getName().startsWith('gson')  &&
        !it.getName().startsWith('support')

    }.collect {
        it.isDirectory() ? it : zipTree(it)
    }
    artifacts.add('archives', task);
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}